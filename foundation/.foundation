
#
# foundation...
#

autoload -Uz edit-command-line
zle -N edit-command-line
bindkey '^X^E' edit-command-line

autoload -Uz compinit && compinit -C
source <(carapace _carapace)

alias stow="stow --target $HOME"
alias vi=nvim
alias lzg=lazygit
alias lzd=lazydocker
alias y=yazi
alias gdu=gdu-go
alias ls="eza --git-ignore"
alias la="ls -lah"
alias lat="la -T"
alias gst="git status"
alias gd="git diff"
alias gp="git push"
alias ga="git add"
#alias gcam="git commit --all --message"
unalias gcam 2>/dev/null

gcam() {
  git add -A

  if git diff --cached --quiet; then
    echo "No changes to commit"
    return 1
  fi

  local diff msg tmp editor_cmd
  diff="$(git diff --cached)"

  msg="$(
    printf "<diff>\n%s\n</diff>\n" "$diff" | claude -p \
"You are an expert software developer writing a concise, informative git commit message.
Analyze the provided git diff enclosed in <diff> tags.
Use conventional commits (e.g., feat: add auth).
First line under 50 chars.
Optional body with bullet points (why + key details).
Return only the commit message.
Do NOT use markdown or code fences (no \`\`\`)." \
    --output-format text
  )" || return 1

  # If fences still appear, remove fence lines
  msg="$(printf "%s\n" "$msg" | sed '/^```/d')"

  tmp="$(mktemp "${TMPDIR:-/tmp}/gcam.XXXXXX")" || return 1
  printf "%s\n" "$msg" > "$tmp"

  editor_cmd="${VISUAL:-${EDITOR:-vi}}"
  "$editor_cmd" "$tmp" || { rm -f "$tmp"; return 1; }

  # Abort if user left it blank/whitespace
  msg="$(cat "$tmp")"
  msg="${msg#"${msg%%[![:space:]]*}"}"
  msg="${msg%"${msg##*[![:space:]]}"}"

  if [ -z "$msg" ]; then
    echo "Commit aborted (empty message)"
    rm -f "$tmp"
    return 1
  fi

  git commit -F "$tmp"
  rm -f "$tmp"
}

set -o vi # enable vim motions in shell text

export EDITOR=vi
export API_TIMEOUT_MS=900000 

source <(fzf --zsh)
export FZF_DEFAULT_COMMAND='fd --type f --strip-cwd-prefix --hidden --follow --exclude .git'
export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
_fzf_compgen_path() {
  fd --hidden --follow --exclude ".git" . "$1"
}
_fzf_compgen_dir() {
  fd --type d --hidden --follow --exclude ".git" . "$1"
}

eval "$(zoxide init zsh --cmd cd)"
eval "$(direnv hook zsh)"
eval "$(starship init zsh)"

